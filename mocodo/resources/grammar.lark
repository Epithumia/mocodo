// NOTE: add a newline at the beginning and at the end of the input before parsing it.
// https://github.com/lark-parser/lark/discussions/1041

%import common.NUMBER
// %ignore /(?<!\n)( |\t)+/ // Ignore all non indented spaces
seq{item}: item (COMMA item)*

COMMA: SP? "," SP?
COLON: SP? ":" SP?
NL.-10: /[^\S\r\n]*\r?\n/
SLASH: "/"
BACKSLASH: "\\"+
LPAREN: SP? "(" SP?
RPAREN: SP? ")" SP?
LBRACKET: SP? "[" SP?
RBRACKET: SP? "]" SP?
HASHTAG: "#"
PERCENT: "%"
SP.-50 : /[^\S\r\n]+/

BOX_NAME : /(?![\W_\d])[^=<>,:\n\\]*[^-\[\\\]=<>,:\s\\]/
_box_attr : attr data_type?
data_type: LBRACKET message? RBRACKET
!box_def_prefix : "+"

ATTR : /(?=\w)(?!_)[^\[\]>,\r\n]*[^\[\]>,\s]/
?message : /[^\r\n\]]+/

start : (break_ | line)*
BREAK :  /(\s*\n)+/
line : indent? (phantoms | comment | clause | NL)
?clause : assoc_clause
        | entity_clause
        | constraint_clause
        | inheritance_clause

PHANTOMS : /( *:)+ */ NL
indent: INDENT
INDENT.10: / +|\t+/
comment : PERCENT /.*\r?\n/

// Associations

assoc_clause: box_def_prefix? assoc_name_def COMMA seq{assoc_leg} (COLON seq{assoc_attr}?)? NL
assoc_name_def : box_name
assoc_leg : _assoc_card? leg_arrow? SP? (LBRACKET leg_role RBRACKET)? entity_name_ref
_assoc_card : card_hidden? card_prefix? card
entity_name_ref: box_name
!card_hidden : "-"
!card_prefix : "_" | "/"
CARD.2 : /(?![-_\/])(\w|\?){2}(?=[ \t]*[^\w,\r\n:])/
LEG_ARROW : /[<>]/
leg_role : message
assoc_attr : _box_attr?

// Entities

entity_clause : box_def_prefix? entity_name_def COLON seq{entity_or_table_attr}? NL
entity_name_def : box_name
entity_or_table_attr : entity_attr_underscore? (_box_attr? | HASHTAG foreign_reference)
!entity_attr_underscore : "_"
foreign_reference : this_table_attr MORETHAN that_table MORETHAN that_table_attr
this_table_attr : attr
that_table_attr : attr
that_table : entity_name_ref
MORETHAN: SP? ">" SP?

// Constraints

constraint_clause : LPAREN constraint_name? RPAREN (LBRACKET constraint_message RBRACKET)? seq{constraint_target}? (COLON constraint_ratios)? NL
CONSTRAINT_NAME : /[\w Â ]{1,3}/
constraint_message: message
constraint_target : constraint_leg? SP* box_name_ref
box_name_ref: box_name
CONSTRAINT_LEG : /<?(\.+|-+)>?/
constraint_ratios : constraint_ratio (COMMA constraint_ratio)?
CONSTRAINT_RATIO : NUMBER

// Inheritance

inheritance_clause : SLASH inheritance_name? BACKSLASH SP? inheritance_parent SP? inheritance_arrow SP? seq{inheritance_child} (COLON seq{assoc_attr})? NL
INHERITANCE_NAME : /(XT\d?|TX\d?|X\d?|T\d?|\d)/
inheritance_parent : entity_name_ref
inheritance_child : entity_name_ref
INHERITANCE_ARROW.-10 : /<==?|<--?|--?>|==?>/

// Avoid anonymous tokens for some terminals

constraint_leg: CONSTRAINT_LEG
card: CARD
constraint_ratio: CONSTRAINT_RATIO
box_name : BOX_NAME
leg_arrow: LEG_ARROW
phantoms: PHANTOMS
break_: BREAK
inheritance_name: INHERITANCE_NAME
constraint_name: CONSTRAINT_NAME
attr: ATTR
inheritance_arrow: INHERITANCE_ARROW
